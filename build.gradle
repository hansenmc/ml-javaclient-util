plugins {
  id "java-library"
  id "maven-publish"
  id "eclipse"
  id "idea"
	id "com.jfrog.bintray" version "1.8.4"
	id "com.github.jk1.dependency-license-report" version "1.3"

	id "net.saliman.properties" version "1.5.1"
	id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

sourceCompatibility = "8"
targetCompatibility = "8"

repositories {
	mavenCentral()
}

dependencies {
	api 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
	api 'com.marklogic:marklogic-client-api:5.5.2'
  api 'com.marklogic:marklogic-xcc:10.0.9'
	api 'org.jdom:jdom2:2.0.6'
	api 'org.slf4j:slf4j-api:1.7.30'
  api 'org.springframework:spring-context:5.2.9.RELEASE'

  testImplementation 'junit:junit:4+'
	testImplementation 'org.springframework:spring-test:5.2.9.RELEASE'

  // Used for testing loading modules from the classpath
  testRuntime files("lib/modules.jar")

	// Forcing Spring to use logback instead of commons-logging
	testRuntime "ch.qos.logback:logback-classic:1.2.3"
	testRuntime group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.30"
	testRuntime group: "org.slf4j", name: "slf4j-api", version: "1.7.30"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from file(javadocsDir + "/" + project.name)
}

javadoc.destinationDir = file(javadocsDir + "/" + project.name)
javadoc.failOnError = false

publishing {
  publications {
    mainJava(MavenPublication) {
      from components.java
    }
    sourcesJava(MavenPublication) {
      from components.java
      artifact sourcesJar
    }
	  mavenJavadoc(MavenPublication) {
		  artifact javadocJar
	  }
  }
}

if (project.hasProperty("myBintrayUser")) {
  bintray {
    user = myBintrayUser
    key = myBintrayKey
    publications = ['mainJava', 'sourcesJava']
    pkg {
      repo = 'maven'
      name = project.name
      licenses = ['Apache-2.0']
      vcsUrl = 'https://github.com/rjrudin/' + project.name + '.git'
      version {
        name = project.version
        released = new Date()
      }
    }
  }
}

test {
  testLogging {
    events 'started','passed', 'skipped', 'failed'
    exceptionFormat 'full'
  }
}

// See https://github.com/snyk/gradle-plugin for more information
snyk {
	//arguments = '--all-sub-projects'
	severity = 'low'
	api = snykToken
	autoDownload = true
	autoUpdate = true
}
